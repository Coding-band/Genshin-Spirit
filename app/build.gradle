/*
 * Project Genshin Spirit (原神小幫手) was
 * Created & Develop by Voc-夜芷冰 , Programmer of Xectorda
 * Copyright © 2022 Xectorda 版權所有
 */

plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 33
    buildToolsVersion "32.0.0"
    def ver
    def verN
    defaultConfig {
        applicationId "com.voc.genshin_helper"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 1
        versionName "3.5.0"

        renderscriptTargetApi 21
        renderscriptSupportModeEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        ver =  versionCode
        verN =  versionName
        ndk{
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64', 'x86'
        }
    }

    def versionPropsFile = rootProject.file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def code = versionProps['VERSION_CODE'].toInteger() + 1
        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        flavorDimensions "version"
        productFlavors {
            // For Develop Testing
            dev {
                versionCode = code
                versionNameSuffix "-dev" + " ("+code+")"
                applicationId "com.voc.genshin_spirit"


            }
            // For Google Play Store
            gp{
                versionCode = code
                versionNameSuffix "-prod" + " ("+code+")"
                applicationId "com.voc.genshin_spirit_gp"
                bundle{
                    language {
                        enableSplit = false;
                    }
                }
            }
            // For Translator and Beta Tester
            beta {
                versionCode = code
                versionNameSuffix "-beta" + " ("+code+")"
                applicationId "com.voc.genshin_spirit_beta"
            }
        }
    }



    buildTypes {
        debug{
            ext.enableBugsnag = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            packagingOptions {
                exclude 'META-INF/LICENSE'
                exclude 'META-INF/LICENSE-FIREBASE.txt'
                exclude 'META-INF/NOTICE'
                exclude 'META-INF/DEPENDENCIES'
            }
            defaultConfig {
                multiDexEnabled true
            }
        }

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "genshin_spirit_${variant.versionName}.apk"
            }
        }
        tasks.whenTaskAdded { task ->
            if (task.name.startsWith("bundle")) {
                def renameTaskName = "rename${task.name.capitalize()}Aab"
                def flavor = task.name.substring("bundle".length()).uncapitalize()
                tasks.create(renameTaskName, Copy) {
                    def path = "${buildDir}/outputs/bundle/${flavor}/"
                    from(path)
                    include "app.aab"
                    destinationDir file("${buildDir}/outputs/renamedBundle/")
                    rename "app.aab", "genshin_spirit_${verN}_${flavor}_${ver}.aab"
                }

                task.finalizedBy(renameTaskName)
            }
//@credit to David Medenjak for this block of code
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }

    bundle{
        language {
            enableSplit = false;
        }
    }
}



dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0'
    implementation 'androidx.navigation:navigation-fragment:2.5.0'
    implementation 'androidx.navigation:navigation-ui:2.5.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'com.google.android.play:core:1.10.3'
    implementation 'com.github.getActivity:XXPermissions:12.6'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.22'
    implementation 'org.commonjava.googlecode.markdown4j:markdown4j:2.2-cj-1.1'
    implementation 'com.github.amikoj:ShadowView:1.0.1'
    implementation 'jp.wasabeef:blurry:4.0.1'
    implementation 'com.github.hackware1993:MagicIndicator:1.7.0' // for androidx
    implementation 'com.github.lzyzsd:circleprogress:1.2.1'
    implementation 'com.mikhaellopez:circularprogressbar:3.1.0'
    implementation "androidx.tonyodev.fetch2:xfetch2:3.1.6"

    implementation("androidx.recyclerview:recyclerview:1.2.1")
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.alibaba.fastjson2:fastjson2:2.0.15.android'
    implementation 'commons-io:commons-io:2.6'

    implementation 'androidx.browser:browser:1.5.0'


}